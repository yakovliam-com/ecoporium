import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'net.ecoporium'
version '1.0.0-SNAPSHOT'
project.ext.name = 'Ecoporium'
project.ext.groupId = 'net.ecoporium.ecoporium'

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = 'https://repo.incendo.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = "https://repo.mattstudios.me/artifactory/public/" }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'

    implementation "net.kyori:adventure-api:4.8.1"
    implementation "net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT"
    implementation "net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT"

    implementation "cloud.commandframework:cloud-bukkit:1.5.0"
    implementation "dev.triumphteam:triumph-gui:3.0.3"

    implementation "com.yahoofinance-api:YahooFinanceAPI:3.15.0"
}

test {
    useJUnitPlatform()
}

// Output to build/libs/shadow.jar
shadowJar {
    archiveBaseName.set("${project.ext.name}-${project.version}")
    archiveClassifier.set('')
    archiveVersion.set('')


    relocate "dev.triumphteam.gui", "${project.ext.groupId}.lib.triumphgui"
    relocate "cloud.commandframework", "${project.ext.groupId}.lib.cloud"
    relocate "io.leangen.geantyref", "${project.ext.groupId}.lib.typetoken"
    relocate "net.kyori", "${project.ext.groupId}.lib.kyori"
    relocate "yahoofinance", "${project.ext.groupId}.lib.yahoofinance"
    relocate "org.slf4j", "${project.ext.groupId}.lib.yahoofinance.slf4j"
    relocate "com.fasterxml.jackson", "${project.ext.groupId}.lib.yahoofinance.jackson"
}

// Replace version information in src/main/resources
processResources {
    filter(ReplaceTokens, tokens: [version: version])
    filter(ReplaceTokens, tokens: [name: project.ext.name])
}