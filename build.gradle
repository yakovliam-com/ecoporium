import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group 'net.ecoporium'
version '1.0.0-SNAPSHOT'
project.ext.name = 'Ecoporium'
project.ext.groupId = 'net.ecoporium.ecoporium'

repositories {
    mavenCentral()
    maven {
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        content {
            includeGroup 'org.bukkit'
            includeGroup 'org.spigotmc'
        }
    }
    maven { url = 'https://repo.incendo.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://oss.sonatype.org/content/repositories/central' }
    maven { url = "https://repo.mattstudios.me/artifactory/public/" }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url 'https://libraries.minecraft.net/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.2'

    compileOnly "org.spigotmc:spigot-api:1.18-R0.1-SNAPSHOT"
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    implementation 'net.kyori:adventure-api:4.9.3'
    implementation 'net.kyori:adventure-platform-bukkit:4.0.1'
    implementation "net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT"
    implementation 'me.lucko:commodore:1.11'
    implementation "dev.triumphteam:triumph-gui:3.0.3"
    implementation "co.aikar:acf-bukkit:0.5.0-SNAPSHOT"
    implementation "de.sfuhrm:YahooFinanceAPI:3.16.2"
    implementation 'com.github.ben-manes.caffeine:caffeine:3.0.5'
    implementation "me.mattstudios:triumph-msg-adventure:2.2.4-SNAPSHOT"
    implementation 'org.spongepowered:configurate-gson:4.1.2'
    implementation "org.jfree:jfreechart:1.5.3"
    implementation files('libs/spigot-maps-2.1.1.jar')
}

test {
    useJUnitPlatform()
}

// Output to build/libs/shadow.jar
shadowJar {
    archiveBaseName.set("${project.ext.name}-${project.version}")
    archiveClassifier.set('')
    archiveVersion.set('')

    dependencies {
        exclude(dependency('com.mojang:brigadier'))
    }

    relocate "dev.triumphteam.gui", "${project.ext.groupId}.lib.triumphgui"
    relocate 'me.mattstudios.msg', "${project.ext.groupId}.lib.msg"
    relocate "net.kyori", "${project.ext.groupId}.lib.kyori"
    relocate "yahoofinance", "${project.ext.groupId}.lib.yahoofinance"
    relocate "com.fasterxml.jackson", "${project.ext.groupId}.lib.yahoofinance.jackson"
    relocate "com.github.benmanes.caffeine", "${project.ext.groupId}.lib.caffeine"
    relocate "me.lucko.commodore", "${project.ext.groupId}.lib.commodore"
    relocate "co.aikar.commands", "${project.ext.groupId}.lib.acf"
}

// Replace version information in src/main/resources
processResources {
    filter(ReplaceTokens, tokens: [version: version])
    filter(ReplaceTokens, tokens: [name: project.ext.name])
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}